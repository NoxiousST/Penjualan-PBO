plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.runtime' version '1.13.0'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://jaspersoft.jfrog.io/jaspersoft/jaspersoft-repo' }
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-core:2.20.0'
    implementation 'com.mysql:mysql-connector-j:8.0.33'
    implementation 'com.jfoenix:jfoenix:9.0.4'
    implementation 'net.sf.jasperreports:jasperreports:6.20.5'
    implementation 'de.jensd:fontawesomefx-materialicons:2.2.0-9.1.2'

    implementation 'com.google.dagger:dagger:2.46.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.46.1'

    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.11'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava3:2.9.0'

    implementation 'commons-validator:commons-validator:1.7'
    implementation group: 'org.apache.poi', name: 'poi', version: '5.2.3'
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.2.3'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    implementation 'com.google.guava:guava:31.0.1-jre'
}

application {
    mainClass = 'me.stiller.Main'
}

tasks.named('test') {
    useJUnitPlatform()
}

javafx {
    version = "20"
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.web', 'javafx.graphics' ]
}

tasks.withType(JavaExec).configureEach {
    jvmArgs([
            '--module-path', 'C:\\javafx-sdk-20.0.1\\lib',
            '--add-modules=javafx.controls,javafx.graphics,javafx.fxml,javafx.web'
    ])
}

def jvmArgsList =
        [
                '--add-exports', 'javafx.base/com.sun.javafx.event=ALL-UNNAMED',
                '--add-exports', 'javafx.base/com.sun.javafx.runtime=ALL-UNNAMED',
                '--add-exports', 'javafx.controls/com.sun.javafx.scene.control.behavior=ALL-UNNAMED',
                '--add-exports', 'javafx.controls/com.sun.javafx.scene.control=ALL-UNNAMED',
                '--add-opens', 'javafx.graphics/javafx.scene=ALL-UNNAMED'
        ]

runtime {
    addModules('java.desktop', 'java.scripting')
    addOptions('--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages')
    jpackage {
        installerName = "project-penjualan-windows-x64"
        imageName = "Project Penjualan"
        version = "1.1.0"
        installerOutputDir = file("C:\\Users\\LENOVO\\Downloads\\Penjualan")
        installerOptions = [
                '--win-per-user-install',
                '--win-dir-chooser',
                '--win-menu',
                '--win-shortcut',
                '--vendor', 'Daniel Kristian',
        ]
        imageOptions = ['--icon', "src/main/resources/me/stiller/images/icon.ico"]
        installerType = "msi"
        jvmArgs += jvmArgsList
    }
}
